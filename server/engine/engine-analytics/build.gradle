plugins {
    id 'nu.studer.jooq' version "${jooqVersion}"
}

dependencies {
    implementation project(':security:security-api')
    implementation project(':engine:engine-core-api')
    implementation project(':common:common-exception-api')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'

    jooqGenerator 'org.postgresql:postgresql'
}

jooq {
    // используем версию jOOQ, определенную плагином Spring Boot Dependency Management
    version = ''
    edition = nu.studer.gradle.jooq.JooqEdition.OSS

    configurations {
        // имя конфигурации может быть любым
        main {
            generateSchemaSourceOnCompilation = true

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = project.ext.POSTGRES_DB_URL
                    user = project.ext.POSTGRES_USER
                    password = project.ext.POSTGRES_PASSWORD
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'moneybox'
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        // пакет для сгенерированных классов
                        packageName = 'by.skyperdyay.engine.analytics.domain.model'
                        // директория генерации
                        directory = 'build/generated-sources/jooq'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}
