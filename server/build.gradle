plugins {
	id 'java'
	id 'org.springframework.boot' version "${springBootVersion}" apply false
	id 'io.spring.dependency-management' version "${dependencyManagementVersion}"
	id 'co.uzzu.dotenv.gradle' version "${uzzuDotenvVersion}"
}

ext['testcontainers.version'] = "${testcontainersVersion}"

// Функция для получения env переменных либо system переменных, при отсутствии env
ext.getEnvVar = { String varName ->
	return env.allVariables()
			.find { key, value -> (varName == key) }
			.getValue()
}

subprojects {
	group = 'by.skuperdyay'
	version = "${appVersion}"

	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'

	// Передаем конкретные переменные
	project.ext.set("POSTGRES_DB_URL", getEnvVar("POSTGRES_DB_URL"))
	project.ext.set("POSTGRES_USER", getEnvVar("POSTGRES_USER"))
	project.ext.set("POSTGRES_PASSWORD", getEnvVar("POSTGRES_PASSWORD"))
	project.ext.set("POSTGRES_DB", getEnvVar("POSTGRES_DB"))

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
		}
	}

	dependencies {
		annotationProcessor 'org.projectlombok:lombok'
	}

	compileJava {
		options.encoding = 'UTF-8'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}